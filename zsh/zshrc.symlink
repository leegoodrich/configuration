# Normal globbing just ain't gonna cut it 
setopt extendedglob

# shortcut to the configuration root
export CONFIG_ROOT=`dirname $(find $HOME -name .configroot -print -quit)`

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files, except anything that shows up in backups
typeset -U config_files
config_files=($CONFIG_ROOT/**/(^backups)/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
